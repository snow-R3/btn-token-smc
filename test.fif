"TonUtil.fif" include
"Asm.fif" include

"auto/minter-ico-v2.func.code.fif" include <s constant code
"config.boc" file>B B>boc constant global-config

"EQDI8skSrGW5EJX6UaU9WP8WY_VaM8cg7gDYFNWsn6olDFcc" parse-smc-addr drop 2=: raw-smc-addr

0x076ef1ea           // magic
0                    // actions
0                    // msgs_sent
1637535065           // unixtime
1                    // block_lt
1                    // trans_lt
239                  // randseed
1000000000 null pair // balance_remaining
<b raw-smc-addr Addr, b> <s  // myself
global-config        // global_config
10 tuple 1 tuple constant c7

0  constant recv-internal // to run recv_internal()
-1 constant recv-external // to invoke recv_external()

"auto/jetton-wallet.func.code.fif" include =: jwallet-code

<b  // TODO: implement new standart
    b{01} s, // offchain#01
    "https://bafkreigdwrwkgjglg36osw2lwjyjagjbf2jb2omfifmyljflesvw3s7gxu.ipfs.cf-ipfs.com/" $,
b>  =: full-content

"EQBAtTjqPOsBvWPO_ij7xkLA11cjiXUKA3gRHVSbrYMEmWOF"
parse-smc-addr drop 2=: owner-addr

<b
    0 Gram,             // supply
    "100" $>GR Gram,       // liq_cap
    "5" $>GR Gram,         // price
    1672402167 32 u,      // end_time
    owner-addr Addr,    // owner_addr
    jwallet-code ref,   // jwall_code
    full-content ref,
b>  =: storage

<b b{1000100} s, owner-addr addr, 0 Gram, b{00} s, b> constant in-msg-cell

"10" $>GR =: my-balance
"10.5" $>GR =: msg-value

<b // op: 2
   0 32 u,
//    now 64 u,
b> <s constant in-msg

my-balance msg-value in-msg-cell in-msg recv-internal code storage c7 runvmctx

constant storage
constant exit-code

."Exit code " exit-code . cr

."supply: " storage <s Gram@+ drop . cr